apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = '0.0.4'
group = 'com.tomasznajda.rxarchitect'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += "src/test/kotlin"
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "android.arch.lifecycle:extensions:$lifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$lifecycleVersion"
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    testImplementation 'junit:junit:4.12'
    testImplementation "com.tomasznajda.ktx:junit:1.0.0"
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'com.tomasznajda.rxarchitect'
                artifactId 'architect'

                name 'rxarchitect'
                description 'Different approach to Android MVP architecture'
                url 'https://github.com/tomasznajda/rxarchitect'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'tomasznajda'
                        name 'Tomasz Najda'
                        email 'hello@tomasznajda.com'
                    }
                }
                scm {
                    connection 'https://github.com/tomasznajda/rxarchitect.git'
                    developerConnection 'https://github.com/tomasznajda/rxarchitect.git'
                    url 'https://github.com/tomasznajda/rxarchitect'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        configurations = ['archives']
        override = true
        pkg {
            repo = 'rxarchitect'
            name = 'rxarchitect'
            desc = 'Different approach to Android MVP architecture'
            websiteUrl = 'https://github.com/tomasznajda/rxarchitect'
            vcsUrl = 'https://github.com/tomasznajda/rxarchitect.git'
            licenses = ["Apache-2.0"]
            publish = true
        }
    }
}
